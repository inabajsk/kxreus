;;;
;;; khr-robot and links for KHR robots
;;;
;;;	2022.8.4 created JSK M.I
;;;
;;(require :eus2wrl)
;;(require :read-wrl)
(require :kxrmodels)


;;; glvertices utility
(defun make-glvertices-from-meshfs (fs &key (material))
  "returns glvertices instance fs is geomatry::faceset"
  (let (mat)
    (cond
     (material (setq mat material))
     ((get fs :face-color)
      (let ((col (get fs :face-color)))
        (unless (vectorp col)
          (setq col (gl::find-color col)))
        (setq mat
              (list (list :ambient (float-vector (elt col 0) (elt col 1) (elt col 2)))
                    (list :diffuse (float-vector (elt col 0) (elt col 1) (elt col 2)))))))
     (t ;; use default material
      (setq mat
            (list (list :ambient (float-vector 0.65 0.65 0.65))
                  (list :diffuse (float-vector 0.65 0.65 0.65))))))
    (make-glvertices-from-meshes (send fs :faces) :material mat)))

(defun make-glvertices-from-meshes (flst &key (material))
  "returns glvertices instance
flst is list of geomatry::face"
  (let ((mat (make-matrix (* 3 (length flst)) 3))
        (nom (make-matrix (* 3 (length flst)) 3))
        (idx (instantiate integer-vector (* 3 (length flst))))
        (cntr 0))
    (dolist (f flst)
      (let ((nm (normalize-vector (send f :normal)))
            (vsl (send f :vertices)))
        ;;
        (user::c-matrix-row mat cntr (car vsl) t)
        (user::c-matrix-row nom cntr nm t)
        (incf cntr)
        ;;
        (user::c-matrix-row mat cntr (cadr vsl) t)
        (user::c-matrix-row nom cntr nm t)
        (incf cntr)
        ;;
        (user::c-matrix-row mat cntr (caddr vsl) t)
        (user::c-matrix-row nom cntr nm t)
        (incf cntr)
        ))
    (dotimes (i (length idx)) (setf (elt idx i) i))
    (let ((msh
           (list (list :type :triangles)
                 (list :vertices mat)
                 (list :normals nom)
                 (list :indices idx))))
      (when material
        (push (list :material material) msh))
      (instance gl::glvertices :init (list msh)))
    ))


(defun create-khr-link-old (link-name &key (joint-coord (make-cascoords)) holes
					(color nil)) ;; #f(0.2 0.2 0.2)))
  (let* ((bs nil) (fname (format nil "glbodies/~A.gvs" link-name))
	 l dat wrl-fname)
    (cond
      ((probe-file fname)
       (with-open-file (f fname :direction :input)
	 (while (setq dat (read f nil nil))
	   (push dat bs)))
       (format t ";; ~A is loaded. length=~A~%" fname (length bs))
       (setq l (kxr-link-create bs :joint-coord joint-coord :color color))       
       )
      (t
       (setq wrl-fname (format nil "~A/KHR/~A.wrl" *rcb4eus-dir* link-name))
       (format t ";; ~A is loaded ~%" wrl-fname)
       (setq bs (wrl2eus wrl-fname 1000.0))
       (format t ";; ~A is loaded finished ~%" wrl-fname)
       (setq dat (mapcar
		  #'(lambda (fs) (let ((glv (make-glvertices-from-meshfs fs))
				       glb)
				   ;;(setq glb (instance gl::glbody :init :faces (send glv :faces)))
				   (setq glb (kxr-make-kxr-body (format nil "~A" (gentemp link-name))
								fs fs nil))
				   (setq (glb . gl::aglvertices) glv)
				   glb))
		  (send bs :bodies)))
       (setq bs (instance bodyset :init (make-cascoords) :bodies dat))
       ;;(apply #'dump-structure fname dat)
       ;;(format t ";; ~A is dumped ~%" fname)
       (setq l (kxr-link-create bs :joint-coord joint-coord :color color))
       ))
    l))


(defun create-wrl-link (link-name &key (joint-coord (make-cascoords)) holes
				    (color nil)) ;; #f(0.2 0.2 0.2)))
  (let* (bs l wrl-fname)
    (setq wrl-fname (format nil "~A/KHR/~A.wrl" *rcb4eus-dir* link-name))
    (setq bs (wrl2eus wrl-fname 1000.0))
    (setq l (kxr-link-create bs :joint-coord joint-coord :color color))       
    l))

(defun create-stl-link (link-name &key (joint-coord (make-cascoords)) holes
				  (color nil)) ;; #f(0.2 0.2 0.2)))
  (let* (bs l stl-fname)
    (setq stl-fname (format nil "~A/stls/~A.stl" *rcb4eus-dir* link-name))
    (setq bs (stl2eus stl-fname :scale 1000.0))
    (setq l (kxr-link-create (list bs) :joint-coord joint-coord :color color))       
    l))

(defun convert-wrl2stl (link-name)
  (let* (bs l wrl-fname)
    (setq wrl-fname (format nil "~A/KHR/~A.wrl" *rcb4eus-dir* link-name))
    (setq bs (wrl2eus wrl-fname 1000.0))
    (eus2stl (format nil "~A/stls/~A.stl" *rcb4eus-dir* link-name) bs)
    ))


(defun khr-docking-pipe ()
  (let* ((s (stl2eus (format nil "~A/stls/KHR_docking_pipe.STL" *rcb4eus-dir*)))
         (l (kxr-make-kxr-body "khr-docking-pipe" s s nil))
         )
    (send l :weight 175.0)
    l))

(defun khr-servo-s2552 nil (khr-servo-krs25 "s2552"))
(defun khr-servo-s2572 nil (khr-servo-krs25 "s2572"))

(defun khr-servo-krs25 (name)
  (let* ((xl/2 (/ 41.0 2))
	 (yl/2 (/ 21.0 2))
	 (zl/2 (/ 30.5 2))
	 (xl- (- xl/2 11.5))
	 (xl+ (+ xl/2 11.5))
	 (f 0.8)
	 (con (make-cube 6 3.5 10))
	 (con- (make-cube 5 3 8))
	 (bod
	  (make-prism
	   (list
	    (float-vector (- f xl-) (- yl/2) (- zl/2))
	    (float-vector (- xl-) (- f yl/2) (- zl/2))
	    (float-vector (- xl-) (- yl/2 f) (- zl/2))
	    (float-vector (- f xl-) yl/2 (- zl/2))
	    (float-vector (- xl+ f) yl/2 (- zl/2))
	    (float-vector xl+ (- yl/2 f) (- zl/2))
	    (float-vector xl+ (- f yl/2) (- zl/2))
	    (float-vector (- xl+ f) (- yl/2) (- zl/2)))
	   (float-vector 0 0 (* 2 zl/2))))
	 (cyl1 (make-cylinder 6.25 32.5))
	 (cyl2 (make-cylinder 3.0 38))
	 (holes (list
		 (list "uph0" #f(0    0 13.0) :z 6.5 8)
		 (list "uph1" #f(0    -10.5 13.0) :-z 1 8)
		 (list "uph2" #f(0     10.5 13.0) :-z 1 8)
		 (list "uph3" #f(21.0  10.5 13.0) :-z 1 8)
		 (list "uph4" #f(21.0 -10.5 13.0) :-z 1 8)
		 (list "downh0" #f(0   0 -13.0) :z 6.5 15)
		 (list "downh1" #f(0    -10.5 -13.0) :z 1 15)
		 (list "downh2" #f(0     10.5 -13.0) :z 1 15)
		 (list "downh3" #f(21.0  10.5 -13.0) :z 1 15)
		 (list "downh4" #f(21.0 -10.5 -13.0) :z 1 15)))
	 (sbod bod)
	 (c (make-cascoords)))
    (send cyl1 :locate (float-vector 0 0 -16.1))
    (send cyl2 :locate (float-vector 0 0 -18.1))
    (setq bod (body+ bod cyl1))
    (setq bod (body+ bod cyl2))
    ;;
    (send con- :locate #f(0 0 -6) :world)
    (send con :assoc con-)
    ;;
    (send con :locate (float-vector 14.5 4.5 -11.5) :world)
    (setq bod (body+ bod con))
    (setq bod (body- bod con-))
    (send con :locate (float-vector 14.5 -4.5 -11.5) :world)
    (setq bod (body+ bod con))
    (setq bod (body- bod con-))
    ;;
    (setq bod (kxr-make-kxr-body name sbod bod holes
                                 :color *kxr-servo-color*))
    (if (substringp "52" name)
	(send bod :put :weight (+ 41.5 1.33))
	(send bod :put :weight (+ 47.7 1.33)))
    (send bod :put :joint-coord (copy-object c))
    (send bod :assoc (get bod :joint-coord))
    (send c :locate #f(0 0 20) :world)
    (send c :put :color #f(0 0 1)) 
    (send c :put :size 30)
    (send bod :put :upper-coord c)
    (send bod :assoc c)
    (send bod :set-color *kxr-servo-color*) ; :black
    bod
    )
  )
(defun khr-servo-dummy (&optional (name "khr-dummy"))
  (let* ((xl/2 (/ 41.0 2))
	 (yl/2 (/ 21.0 2))
	 (zl/2 (/ 30.5 2))
	 (xl- (- xl/2 11.5))
	 (xl+ (+ xl/2 11.5))
	 (f 0.8)
	 (bod0
	  (make-prism
	   (list
	    (float-vector (- f xl-) (- yl/2) (- zl/2))
	    (float-vector (- xl-) (- f yl/2) (- zl/2))
	    (float-vector (- xl-) (- yl/2 f) (- zl/2))
	    (float-vector (- f xl-) yl/2 (- zl/2))
	    (float-vector (- xl+ f) yl/2 (- zl/2))
	    (float-vector xl+ (- yl/2 f) (- zl/2))
	    (float-vector xl+ (- f yl/2) (- zl/2))
	    (float-vector (- xl+ f) (- yl/2) (- zl/2)))
	   (float-vector 0 0 (* 2 zl/2))))
	 (bod (make-cube 41 21 30))
	 (bod- (make-cube 39 19 30))
	 (cyl0 (make-cylinder 4 100))
	 (cyl1 (make-cylinder 8 8))
	 (holes (list
		 (list "uph0" #f(0    0 13.0) :z 6.5 8)
		 (list "uph1" #f(0    -10.5 13.0) :-z 1 8)
		 (list "uph2" #f(0     10.5 13.0) :-z 1 8)
		 (list "uph3" #f(21.0  10.5 13.0) :-z 1 8)
		 (list "uph4" #f(21.0 -10.5 13.0) :-z 1 8)
		 (list "downh0" #f(0   0 -13.0) :z 6.5 15)
		 (list "downh1" #f(0    -10.5 -13.0) :z 1 15)
		 (list "downh2" #f(0     10.5 -13.0) :z 1 15)
		 (list "downh3" #f(21.0  10.5 -13.0) :z 1 15)
		 (list "downh4" #f(21.0 -10.5 -13.0) :z 1 15)))
	 (sbod bod)
	 (c (make-cascoords)))
    (send cyl0 :rotate pi/2 :y :world)
    (send cyl0 :locate #f(-30 0 0) :world)
    (setq bod (body- bod cyl0))
    (send cyl1 :locate (float-vector -11.5 0 11.5))
    (setq bod (body+ bod cyl1))
    (send bod- :locate #f(0 0 -2) :world)
    (setq bod (body- bod bod-))
    (send bod :locate #f(11.5 0 0) :world)
    ;;    (send bod :locate #f(11.5 0 15) :world)
    ;;
    (setq bod (kxr-make-kxr-body name sbod bod holes
                                 :color *kxr-servo-color*))
    (if (substringp "52" name)
	(send bod :put :weight (+ 41.5 1.33))
	(send bod :put :weight (+ 47.7 1.33)))
    (send bod :put :joint-coord (copy-object c))
    (send bod :assoc (get bod :joint-coord))
    (send c :locate #f(0 0 20) :world)
    (send c :put :color #f(0 0 1)) 
    (send c :put :size 30)
    (send bod :put :upper-coord c)
    (send bod :assoc c)
    (send bod :set-color *kxr-servo-color*) ; :black
    bod
    )
  )

(defun khr-head-link0-wrl ()  ;; KHR original head
  (create-wrl-link "HEAD_LINK0" :joint-coord (make-cascoords :pos #f(0 0 41))))
(defun khr-head-link0 ()  ;; KHR original head
  (create-stl-link "HEAD_LINK0" :joint-coord (make-cascoords :pos #f(0 0 41))))
(defun khr-head-link1-wrl () ;; arm to :z+
  (create-wrl-link "HEAD_LINK1" :joint-coord (make-cascoords :pos #f(0 0 25))))
(defun khr-head-link1 () ;; arm to :z+
  (khr-arm-link))
(defun khr-head-link2-wrl (&key (joint-coord (make-cascoords :pos #f(-5 0 13.5)))
			     (color #f(0 0 1)))
  (create-wrl-link "HEAD_LINK2" :joint-coord joint-coord :color #f(0.2 0.2 0.2)))
(defun khr-head-link2 (&key (joint-coord (make-cascoords :pos #f(-5 0 13.5)))
			 (color  #f(0.2 0.2 0.2)))
  (let ((l (khr-servo-s2552))
	)
    (send l :rotate pi :z)
    (send l :rotate pi/2 :x)
    (setq l (kxr-link-create (list l) :joint-coord joint-coord :color color))
    l))
(defun khr-head-link3-wrl () ;;  h2s(M5stickv x 2) + m5unitv2
  (create-wrl-link "HEAD_LINK3" :joint-coord (make-cascoords :pos #f(0 0 60))))
(defun khr-head-link3 (&key (joint-coord (make-cascoords :pos #f(-5 0 0)))
			 (color  #f(0.2 0.2 0.2)))
  (let* ((base (kxr-yaw-double-servo-flat-link :base nil))
	 (eye-length 20)
	 (leye (kxr-m5stickv-eye-link :l/r :leye :len eye-length
				      :front nil
				      :vertical nil
				      :frame :high))
	 (reye (kxr-m5stickv-eye-link :l/r :reye :len eye-length
				      :front nil
				      :vertical nil
				      :frame :high))
	 (ceye (m5unitv2-bodyset))
	 l)
    (send leye :locate (send (get base :leye) :worldpos) :world)
    (send leye :rotate (deg2rad -7) :z :world)
    (send reye :locate (send (get base :reye) :worldpos) :world)
    (send reye :rotate (deg2rad 7) :z :world)
    (send ceye :rotate pi :z :world)
    (send ceye :locate #f(33 0 -10) :world)
    (setq l (kxr-link-create (append (send base :bodies)
				     (send leye :bodies)
				     (send reye :bodies)
				     (send ceye :bodies)) :joint-coord joint-coord :color color))    
    l))
;;(defun khr-body-link-all () (create-wrl-link "BODY")) ;; 17 axis whole heavy
(defun khr-l2l5body-link-wrl ()  ;; 17 axis whole body
  (let* ((w (create-wrl-link "BODY_min")) ;; "BODY" "BODY_CHEST_min"
	 (c (create-wrl-link "CHEST_LINK0_min"))
	 (l (kxr-link-create (append (send w :bodies) (send c :bodies)))))
    (setf (get l :head) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :neck) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :larm) (make-cascoords :pos (float-vector 11.5 49 47.4)))
    (setf (get l :rarm) (make-cascoords :pos (float-vector 11.5 -49 47.4)))
    (setf (get l :lleg) (make-cascoords :pos (float-vector 18.5 17 -62.65)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 18.5 -17 -62.65)))
    l))
(defun khr-l2l5body-link ()  ;; 17 axis whole body
  (let* ((w (khr-waist17-link))
	 (c (khr-chest-link))
	 (l (kxr-link-create (append (send w :bodies) (send c :bodies)))))
    (setf (get l :head) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :neck) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :larm) (make-cascoords :pos (float-vector 11.5 49 47.4)))
    (setf (get l :rarm) (make-cascoords :pos (float-vector 11.5 -49 47.4)))
    (setf (get l :lleg) (make-cascoords :pos (float-vector 18.5 17 -62.65)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 18.5 -17 -62.65)))
    l))
(defun khr-l2l6body-link ()  ;; 17 axis whole body
  (let* ((w (khr-waist22-link :center-dummy t))
	 (c (khr-chest-link))
	 (l (kxr-link-create (append (send w :bodies) (send c :bodies)))))
    (setf (get l :head) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :neck) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :larm) (make-cascoords :pos (float-vector 11.5 49 47.4)))
    (setf (get l :rarm) (make-cascoords :pos (float-vector 11.5 -49 47.4)))
    (setf (get l :lleg) (make-cascoords :pos (float-vector 0 22 -40)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 0 -22 -40)))
    l))
;;(defun khr-body-link () (create-wrl-link "BODY17")) ;; 17 axis whole body broken
(defun khr-waist17-link-wrl ()  ;; 17 axis whole body
  (let ((l (create-wrl-link "BODY_min")) ;; "BODY" "BODY_CHEST_min"
	)
    (setf (get l :lleg) (make-cascoords :pos (float-vector 18.5 17 -62.65)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 18.5 -17 -62.65)))
    l))
(defun khr-lleg-crotch-arm-wrl (&key (joint-coord (make-cascoords :pos #f(0 0 0))) (color  #f(0.2 0.2 0.2)))
  (let* ((a (create-stl-link "LLEG_CROTCH_ARM"))
	 (l
	  (kxr-link-create (send a :bodies) :joint-coord joint-coord :color color)))
    l))
(defun khr-lleg-crotch-arm (&key (joint-coord (make-cascoords :pos #f(0 0 0))) (color  #f(0.2 0.2 0.2)))
  (let* ((a (create-stl-link "LLEG_CROTCH_ARM"))
	 (l
	  (kxr-link-create (send a :bodies)
			   :joint-coord joint-coord :color color)))
    l))
(defun khr-rleg-crotch-arm-wrl (&key (joint-coord (make-cascoords :pos #f(0 0 0))) (color  #f(0.2 0.2 0.2)))
  (let* ((a (create-stl-link "RLEG_CROTCH_ARM"))
	 (l
	  (kxr-link-create (send a :bodies) :joint-coord joint-coord :color color)))
    l))
(defun khr-rleg-crotch-arm (&key (joint-coord (make-cascoords :pos #f(0 0 0))) (color  #f(0.2 0.2 0.2)))
  (let* ((a (create-stl-link "RLEG_CROTCH_ARM"))
	 (l
	  (kxr-link-create (send a :bodies) :joint-coord joint-coord :color color)))
    l))
(defun khr-waist17-link (&key (joint-coord (make-cascoords)) (color  #f(0.2 0.2 0.2)))
  (let ((sc (khr-servo-dummy))
	(sr (khr-servo-dummy))
	(sl (khr-servo-dummy))
	(ra (create-stl-link "RLEG_CROTCH_ARM"))
	(la (create-stl-link "LLEG_CROTCH_ARM"))
	l)
    (send sc :locate #f(0 0 -20) :world)
    (send sc :rotate pi :z :world)
    ;;
    (send sr :locate #f(0 -22 -20) :world)
    (send sr :rotate pi :z :world)
    (send sr :rotate pi :x :world)
    ;;
    (send sl :locate #f(0 22 -20) :world)
    (send sl :rotate pi :z :world)
    (send sl :rotate pi :x :world)
    ;;
    (setq l (kxr-link-create (append
			      (list sc sr sl)
			      (send ra :bodies)
			      (send la :bodies)) :joint-coord joint-coord :color color))
    (setf (get l :lleg) (make-cascoords :pos (float-vector 18.5 17 -62.65)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 18.5 -17 -62.65)))
    l))
(defun khr-waist22-link-wrl ()  ;; 17 axis whole body
  (let ((l (create-wrl-link "BODY22_min")) ;; "BODY" "BODY_CHEST_min"
	)
    (setf (get l :lleg) (make-cascoords :pos (float-vector 0 21 -40)))
    (setf (get l :rleg) (make-cascoords :pos (float-vector 0 -21 -40)))
    l))
(defun khr-waist22-link (&key (joint-coord (make-cascoords)) (color  #f(0.2 0.2 0.2))
			   center-dummy)
  (let ((sc (if center-dummy (khr-servo-dummy) (khr-servo-s2552)))
	(sr (khr-servo-s2552))
	(sl (khr-servo-s2552))
	l)
    (send sc :locate #f(0 0 -20) :world)
    (send sc :rotate pi :z :world)
    ;;
    (send sr :locate #f(0 -22 -20) :world)
    (send sr :rotate pi :z :world)
    (send sr :rotate pi :x :world)
    ;;
    (send sl :locate #f(0 22 -20) :world)
    (send sl :rotate pi :z :world)
    (send sl :rotate pi :x :world)
    ;;
    (setq l (kxr-link-create (list sc sr sl)
			     :joint-coord joint-coord :color color))
    (setf (get l :torso) (get sc :upper-coord))
    (setf (get l :lleg) (get sl :upper-coord))
    (setf (get l :rleg) (get sr :upper-coord))
    l))
(defun khr-chest-link-wrl ()
  (let ((l (create-wrl-link "CHEST_LINK0_min")))
    (setf (get l :head) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :neck) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :larm) (make-cascoords :pos (float-vector 11.5 49 47.4)))
    (setf (get l :rarm) (make-cascoords :pos (float-vector 11.5 -49 47.4)))
    l))
(defun khr-backpack-link (&key (name "khr-back-pack")
			    (joint-coord (make-cascoords)) (color  #f(0.2 0.2 0.2)))
  (let ((bod (make-cube 50 76 68)) ;; z:88, 10
	(bodr (make-cube 50 15 88)) ;; side
	(bodl (make-cube 50 15 88)) ;; side
	(b- (make-cube 100 75 20)) ;; y:73
	(c- (make-cube 24 200 50))
	(c0u- (make-cube 24 200 50))
	(c0d- (make-cube 48 200 20))
	(cboard (make-cascoords :name "cboard"))
	l)
    (send c- :rotate (/ pi 4) :y :world)
    (send c- :locate #f(-24 0 44) :world)
    (setq bodr (body- bodr c-))
    (setq bodl (body- bodl c-))
    (setq bod (body- bod c-))
    (send c- :rotate (/ pi -2) :y :world)
    (send c- :locate #f(-24 0 -44) :world)
    (setq bodr (body- bodr c-))
    (setq bodl (body- bodl c-))
    (setq bod (body- bod c-))
    (send c0u- :rotate (deg2rad -60) :y :world)
    (send c0u- :locate #f(24 0 44) :world)
    (setq bodr (body- bodr c0u-))
    (setq bodl (body- bodl c0u-))
    (send c0d- :locate #f(24 0 -44) :world)
    (setq bodr (body- bodr c0d-))
    (setq bodl (body- bodl c0d-))
    ;;
    (send bodr :locate #f(-1 -45 0) :world)
    (send bodl :locate #f(-1 45 0) :world)
    (send bod :assoc bodr)
    (send bod :assoc bodl)
    ;;
    (send bod :locate #f(-54 0 40) :world)
    (setq bod (kxr-make-kxr-body name bod bod nil :color color))
    (setf (get bod :weight) 66.0)
    (send cboard :rotate pi :z :world)
    (send cboard :rotate -pi/2 :y :world)
    (send cboard :locate (float-vector -32 26 5.5) :world)
    (send bod :assoc cboard)
    (setq l (kxr-link-create (list bod bodr bodl) :joint-coord joint-coord :color color))
    l))
(defun khr-chest-link (&key (joint-coord (make-cascoords)) (color  #f(0.2 0.2 0.2)))
  (let ((bod (make-cube 52 88 60))
	(b- (make-cube 50 100 54))
	(b0- (make-cube 100 29 20))
	(b1- (make-cube 40 100 10))
	(s- (make-cube 70 12 72))
	(xs 11.5)
	(zs 17.5)
	(sc (khr-servo-s2552))
	(sr (khr-servo-s2552))
	(sl (khr-servo-s2552))
	(ba (khr-backpack-link))
	l)
    ;;
    (send s- :locate #f(0 43.5 -30) :world)
    (setq bod (body- bod s-))
    (send s- :locate #f(0 -43.5 -30) :world)
    (setq bod (body- bod s-))
    (send b- :locate #f(0 0 2) :world)
    (setq bod (body- bod b-))
    (send b0- :locate #f(0 0 30) :world)
    (setq bod (body- bod b0-))
    (send b1- :locate #f(0 0 30) :world)
    (setq bod (body- bod b1-))
    ;;
    (send sc :rotate pi :z)
    (send sc :locate (float-vector xs 0 14.7) :world)
    (send bod :assoc sc)
    (send sr :rotate pi :z)
    (send sr :rotate pi/2 :x :world)
    (send sr :locate (float-vector xs (- -44 -15) zs) :world)
    (send bod :assoc sr)
    (send sl :rotate pi :z)
    (send sl :rotate -pi/2 :x :world)
    (send sl :locate (float-vector xs (- 44 15) zs) :world)
    (send bod :assoc sl)
    ;;
    (send bod :locate #f(0 0 30) :world)
    (setq l (kxr-link-create (append (list bod sc sr sl)
				     (send ba :bodies))
			     :joint-coord joint-coord :color color))
    (setf (get l :head) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :neck) (make-cascoords :pos (float-vector 11.5 0 64)))
    (setf (get l :larm) (make-cascoords :pos (float-vector 11.5 49 47.4)))
    (setf (get l :rarm) (make-cascoords :pos (float-vector 11.5 -49 47.4)))
    l))
;;(defun khr-chest-link-heavy () (create-wrl-link "CHEST_LINK0")) ;; heavy

(defun khr-larm-link0-wrl (&optional (pos (float-vector 19.75 25.5 0)))
  (create-wrl-link "LARM_LINK0" :joint-coord (make-cascoords :pos pos)))
(defun khr-rarm-link0-wrl (&optional (pos (float-vector 19.75 -25.5 0)))
  (create-wrl-link "RARM_LINK0" :joint-coord (make-cascoords :pos pos)))
(defun khr-larm-link0 (&optional (pos (float-vector 19.75 25.5 0)))
  (let ((am (khr-arm-link)) l)
    (send am :rotate pi/2 :z :world)
    (send am :rotate -pi/2 :x :world)
    (setq l (kxr-link-create (send am :bodies) :joint-coord (make-cascoords :pos pos)))
    l))
(defun khr-rarm-link0 (&optional (pos (float-vector 19.75 -25.5 0)))
  (let ((am (khr-arm-link)) l)
    (send am :rotate pi/2 :z :world)
    (send am :rotate pi/2 :x :world)
    (setq l (kxr-link-create (send am :bodies) :joint-coord (make-cascoords :pos pos)))
    l)
  )

(defun khr-larm-link10-wrl (&optional (pos (float-vector 19.75 -25.5 0)))
  (create-wrl-link "LARM_LINK10" :joint-coord (make-cascoords :pos pos)))
(defun khr-rarm-link10-wrl (&optional (pos (float-vector 19.75 -25.5 0)))
  (create-wrl-link "RARM_LINK10" :joint-coord (make-cascoords :pos pos)))

(defun khr-larm-link10 (&optional (pos (float-vector -16 0 -75)))
  (let ((sv0 (khr-servo-s2552))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(-20 0 0) :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate (float-vector -17 0 -54) :world)
    (setq l (kxr-link-create (list sv0 sv1) :joint-coord (make-cascoords :pos pos)))    
    ))
(defun khr-rarm-link10 (&optional (pos (float-vector -16 0 -75)))
  (let ((sv0 (khr-servo-s2552))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(-20 0 0) :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate (float-vector -17 0 -54) :world)
    (setq l (kxr-link-create (list sv0 sv1) :joint-coord (make-cascoords :pos pos)))    
    ))
;;
(defun khr-larm-link1-wrl (&optional (pos (float-vector -15.3 -19.7 -87.5)))
  (create-wrl-link "LARM_LINK1" :joint-coord (make-cascoords :pos pos)))
(defun khr-rarm-link1-wrl (&optional (pos (float-vector -15.3 19.7 -87.5)))
  (create-wrl-link "RARM_LINK1" :joint-coord (make-cascoords :pos pos)))

(defun khr-larm-link1 (&optional (pos (float-vector -15.3 -19.7 -87.5)))
  (let ((sv0 (khr-servo-s2552))
	(dummy (khr-servo-dummy))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(-20 0 0) :world)
    (send dummy :rotate pi :y :world)
    (send dummy :locate (float-vector -17 0 -54) :world)
    (send sv1 :rotate pi/2 :x :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate #f(-16 0 -87) :world)
    (setq l (kxr-link-create (list sv0 dummy sv1) :joint-coord (make-cascoords :pos pos)))    
    ))
(defun khr-rarm-link1 (&optional (pos (float-vector -15.3 19.7 -87.5)))
  (let ((sv0 (khr-servo-s2552))
	(dummy (khr-servo-dummy))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(-20 0 0) :world)
    (send dummy :rotate pi :y :world)
    (send dummy :locate (float-vector -17 0 -54) :world)
    (send sv1 :rotate -pi/2 :x :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate #f(-16 0 -87) :world)
    (setq l (kxr-link-create (list sv0 dummy sv1) :joint-coord (make-cascoords :pos pos)))    
    ))

(defun khr-larm-link11-wrl (&optional (pos (float-vector -19.75 0 -75.0)))
  (create-wrl-link "LARM_LINK11" :joint-coord (make-cascoords :pos pos)))
(defun khr-rarm-link11-wrl (&optional (pos (float-vector -19.75 0 -75.0)))
  (create-wrl-link "RARM_LINK11" :joint-coord (make-cascoords :pos pos)))
(defun khr-larm-link11 (&optional (pos (float-vector 5 -20.0 -13.5)))
  (let ((sv1 (khr-servo-s2552))
	l)
    (send sv1 :rotate pi/2 :x :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate #f(5 0 -13.5) :world)
    (setq l (kxr-link-create (list sv1) :joint-coord (make-cascoords :pos pos)))    
    l))
(defun khr-rarm-link11 (&optional (pos (float-vector 5 20.0 -13.5)))
  (let ((sv1 (khr-servo-s2552))
	l)
    (send sv1 :rotate -pi/2 :x :world)
    (send sv1 :rotate pi :y :world)
    (send sv1 :locate #f(5 0 -13.5) :world)
    (setq l (kxr-link-create (list sv1) :joint-coord (make-cascoords :pos pos)))    
    l))

(defun khr-larm-link2-wrl (&optional (pos (float-vector 0 0 -65.0)))
  (create-wrl-link "LARM_LINK2" :joint-coord (make-cascoords :pos pos)))
(defun khr-rarm-link2-wrl (&optional (pos (float-vector 0 0 -65.0)))
  (create-wrl-link "RARM_LINK2" :joint-coord (make-cascoords :pos pos)))

(defun khr-larm-link2 ()
  (let* ((am (khr-arm-link))
	 (c (make-cascoords)))
    (send am :assoc c)
    (send am :rotate pi :z :world)
    (send am :locate (float-vector 0 20 -25) :world)
    (kxr-link-create (send am :bodies) :joint-coord c)))
(defun khr-rarm-link2 ()
  (let ((am (khr-arm-link))
	(c (make-cascoords)))
    (send am :assoc c)
    (send am :locate (float-vector 0 -20 -25) :world)
    (kxr-link-create (send am :bodies) :joint-coord c)))
;;
(defun khr-lwrist-link (&optional (pos (float-vector 0 0 -41)))
  (let ((dummy (khr-servo-dummy)))
    (send dummy :rotate -pi/2 :x :world)
    (send dummy :rotate pi/2 :y :world)
    (send dummy :locate #f(0 0 -9) :world)
    (kxr-link-create (list dummy)
		     :joint-coord (make-cascoords :pos pos))))
(defun khr-rwrist-link (&optional (pos (float-vector 0 0 -41)))
  (let ((dummy (khr-servo-dummy)))
    (send dummy :rotate pi/2 :x :world)
    (send dummy :rotate pi/2 :y :world)
    (send dummy :locate #f(0 0 -9) :world)
    (kxr-link-create (list dummy)
		     :joint-coord (make-cascoords :pos pos))))
;;
(defun khr-larm-hand-wrl (&optional (pos (float-vector 0 -4 -35.0)))
  (let* ((wrl-fname (format nil "~A/KHR/~A.wrl" *rcb4eus-dir* "LARM_HAND"))
	 (bs (wrl2eus wrl-fname 1000.0)))
    (send bs :locate #f(0 -20 66) :world)
    (kxr-link-create (send bs :bodies)
		     :joint-coord (make-cascoords :pos pos))))
(defun khr-hand-palm nil
  (let*
      ((x -11)
       (bod (make-prism
	     (list (float-vector x 11 0)
		   (float-vector x 11 -7)
		   (float-vector x 9 -7)
		   (float-vector x 9 -13)
		   (float-vector x 7 -15)
		   (float-vector x 7 -22)
		   (float-vector x -2 -31)
		   (float-vector x -4 -32)
		   (float-vector x -5 -30)
		   (float-vector x 1 -20)
		   (float-vector x 1 -10)
		   (float-vector x -7 -10)
		   (float-vector x -7 0))
	     (float-vector (* -2 x) 0 0)))
       )
    (send bod :locate #f(0 0 -3) :world)
    bod))

(defun khr-hand-finger nil
  (let* ((y 3)
	 (bod
	  (make-prism
	   (list (float-vector -11 y 0)
		 (float-vector  11 y 0)
		 (float-vector  11 y -2)
		 (float-vector  16 y -10)
		 (float-vector  16 y -21)
		 (float-vector  11 y -21)
		 (float-vector  11 y -12)
		 (float-vector  0 y -7)
		 (float-vector  -11 y -7)
		 )
	   (float-vector 0 (* -2 y) 0))))
    (send bod :locate #f(0 0 -3) :world)
    bod))

(defun khr-larm-hand (&optional (pos (float-vector 0 -4 -35.0)))
  (let* ((cyl (make-cylinder 7 4))
	 (palm (khr-hand-palm))
	 (finger (khr-hand-finger)))
    (send cyl :locate #f(0 0 -4) :world)
    (send finger :locate #f(0 -10 -4) :world)
    (kxr-link-create (list cyl palm finger)
		     :joint-coord (make-cascoords :pos pos))))
    
(defun khr-rarm-hand-wrl (&optional (pos (float-vector 0 4 -35.0)))
  (let* ((wrl-fname (format nil "~A/KHR/~A.wrl" *rcb4eus-dir* "RARM_HAND"))
	 (bs (wrl2eus wrl-fname 1000.0)))
    (send bs :locate #f(0 20 66) :world)
    (kxr-link-create (send bs :bodies)
		     :joint-coord (make-cascoords :pos pos))))
(defun khr-rarm-hand (&optional (pos (float-vector 0 -4 -35.0)))
  (let* ((cyl (make-cylinder 7 4))
	 (palm (khr-hand-palm))
	 (finger (khr-hand-finger)))
    (send palm :rotate pi :z :world)
    (send cyl :locate #f(0 0 -4) :world)
    (send finger :locate #f(0 10 -4) :world)
    (kxr-link-create (list cyl palm finger)
		     :joint-coord (make-cascoords :pos pos))))

(defun khr-hand-link (&key (l/r :larm))
  (if (eq l/r :larm) (khr-larm-hand) (khr-rarm-hand)))
;;
(defun khr-arm-link-wrl nil
  (let ((l (create-wrl-link "HEAD_LINK1")))
    l))
(defun khr-joint-base nil
  (let* ((bod (make-cube 18 40 6))
	 (cyl (make-cylinder 16 3.5))
	 l)
    (send cyl :locate #f(0 0 -3) :world)
    (setq l (kxr-link-create (list bod cyl)))
    l)
  )
(defun khr-arm-link (&key (joint-coord (make-cascoords :pos #f(0 0 25))))
  (let ((au (kxr-arm-aux 25 :name "khr-arm-upper" :upper t :radius 9
			 :depth 6 :thickness 4.5 :shrink 0))
	(ab (kxr-arm-aux 25 :name "khr-arm-bottom" :upper t :radius 9
			 :depth 6 :thickness 4.5 :shrink 0))
	(bod (khr-joint-base))
	l)
    (send au :rotate -pi/2 :z :world)
    (send au :locate #f(0 -16.5 0) :world)
    (send ab :rotate pi/2 :z :world)
    (send ab :locate #f(0 16.5 0) :world)
    (send bod :assoc au)
    (send bod :assoc ab)
    (send bod :locate #f(0 0 3) :world)
    (setq l (kxr-link-create (append (send bod :bodies) (list au ab)) :joint-coord joint-coord))
    l))

(defun khr-lleg-link00-wrl (&optional (pos (float-vector 0 21 -40))) ;; -15.3 19.7 -87.5)))
  (create-wrl-link "LLEG_LINK00" :joint-coord (make-cascoords :pos pos)))
(defun khr-leg-link00-base nil
  (let ((y -9.6))
    (make-prism
     (list
      (float-vector 13.8  y 0)
      (float-vector -16 y  0)
      (float-vector -23.9  y -8)
      (float-vector -22  y -8)
      (float-vector -15.7  y -1.8)
      (float-vector 13.5  y -1.8)
      (float-vector 18.8   y -8)
      (float-vector 20.5 y -8))
     (float-vector 0 19.2 0))))
(defun khr-leg-link00-flat nil
  (let ((y -9.6)
	(x -0.85))
    (make-prism
     (list
      (float-vector x y 0)
      (float-vector x (- y) 0)
      (float-vector x (- y) -2)
      (float-vector x 4 -15)
      (float-vector x 2 -20)
      (float-vector x -1 -22)
      (float-vector x -3 -23)
      (float-vector x -8 -22)
      (float-vector x -11 -20)
      (float-vector x -12.2 -17)
      (float-vector x y -2))
     (float-vector (* -2 x) 0 0))))

(defun khr-lleg-link00 (&optional (pos (float-vector 20 -5 -23.5)))
  (let* ((bod (khr-leg-link00-base))
	 (flatf (khr-leg-link00-flat))
	 (flatb (copy-object flatf))
	 )
    (send flatf :locate #f(19.7 0 -8) :world)
    (send flatb :locate #f(-23.1 0 -8) :world)
    (kxr-link-create (list bod flatf flatb) :joint-coord (make-cascoords :pos pos))
    ))
    
(defun khr-rleg-link00-wrl (&optional (pos (float-vector 0 -21 -40)))
  (create-wrl-link "RLEG_LINK00" :joint-coord (make-cascoords :pos pos)))
(defun khr-rleg-link00 (&optional (pos (float-vector 20 5 -23.5)))
  (let* ((bod (khr-leg-link00-base))
	 (flatf (khr-leg-link00-flat))
	 (flatb (copy-object flatf))
	 )
    (send flatf :rotate pi :z :world)
    (send flatb :rotate pi :z :world)
    (send flatf :locate #f(19.7 0 -8) :world)
    (send flatb :locate #f(-23.1 0 -8) :world)
    (kxr-link-create (list bod flatf flatb) :joint-coord (make-cascoords :pos pos))
    ))
(defun khr-lleg-link0-wrl (&optional (pos (float-vector -21 -8.25 -38.5)))
  (create-wrl-link "LLEG_LINK0" :joint-coord (make-cascoords :pos pos)))
(defun khr-lleg-link0 (&key (joint-coord (make-cascoords :pos (float-vector -21 -8.25 -38.5)))
			 (color  #f(0.2 0.2 0.2)))
  (let ((sv (khr-servo-s2552)) 
	(am (khr-arm-link))
	l)
    (send sv :rotate pi/2 :y :world)
    (send sv :rotate pi/2 :x :world)
    (send sv :locate #f(-20 0 0) :world)
    (send am :rotate pi :x :world)
    (send am :locate (float-vector -20 11.5 -14) :world)
    (setq l (kxr-link-create (append (list sv) (send am :bodies))
			     :joint-coord joint-coord :color color))    
    l))
(defun khr-rleg-link0-wrl (&optional (pos (float-vector -21 8.25 -38.5)));; 20 -17 -62.65)))
  (create-wrl-link "RLEG_LINK0" :joint-coord (make-cascoords :pos pos)))
(defun khr-rleg-link0 (&key (joint-coord (make-cascoords :pos (float-vector -21 8.25 -38.5)))
			 (color  #f(0.2 0.2 0.2)))
  (let ((sv (khr-servo-s2552)) 
	(am (khr-arm-link))
	l)
    (send sv :rotate pi/2 :y :world)
    (send sv :rotate -pi/2 :x :world)
    (send sv :locate #f(-20 0 0) :world)
    (send am :rotate pi :x :world)
    (send am :locate (float-vector -20 -11.5 -14) :world)
    (setq l (kxr-link-create (append (list sv) (send am :bodies))
			     :joint-coord joint-coord :color color))    
    l))

(defun khr-lleg-link1-wrl (&optional (pos (float-vector 0 0 -65)))
  (create-wrl-link "LLEG_LINK1" :joint-coord (make-cascoords :pos pos)))
(defun khr-lleg-link1 (&optional (pos (float-vector 0 0 -65)))
  (let ((sv0 (khr-servo-s2552))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate pi/2 :x :world)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(0 20 0) :world)
    (send sv1 :rotate pi/2 :x :world)
    (send sv1 :locate #f(0 20 -65) :world)
    (send sv1 :rotate (deg2rad -60) :y :world)
    (setq l (kxr-link-create (list sv0 sv1)
			     :joint-coord (make-cascoords :pos pos)))        
    l))
(defun khr-rleg-link1-wrl (&optional (pos (float-vector 0 0 -65)))
  (create-wrl-link "RLEG_LINK1" :joint-coord (make-cascoords :pos pos)))

(defun khr-rleg-link1 (&optional (pos (float-vector 0 0 -65)))
  (let ((sv0 (khr-servo-s2552))
	(sv1 (khr-servo-s2552))
	l)
    (send sv0 :rotate -pi/2 :x :world)
    (send sv0 :rotate pi/2 :y :world)
    (send sv0 :locate #f(0 -20 0) :world)
    (send sv1 :rotate -pi/2 :x :world)
    (send sv1 :locate #f(0 -20 -65) :world)
    (send sv1 :rotate (deg2rad -60) :y :world)
    (setq l (kxr-link-create (list sv0 sv1)
			     :joint-coord (make-cascoords :pos pos)))        
    l))

(defun khr-lleg-link2-wrl (&optional (pos (float-vector 0 0 -65)))
  (create-wrl-link "LLEG_LINK2" :joint-coord (make-cascoords :pos pos)))
(defun khr-lleg-link2 (&optional (pos (float-vector 0 0 -65)))
  (let ((am (khr-arm-link))
	(sv (khr-servo-s2552))
	l)
    (send am :rotate (deg2rad -30) :y :world)
    (send am :locate #f(0 20 -25))
    (send sv :rotate pi/2 :x :world)
    (send sv :rotate (deg2rad -70) :y :world)
    (send sv :locate (float-vector 0 20 -65) :world)
    (setq l (kxr-link-create (append (send am :bodies) (list sv))
			     :joint-coord (make-cascoords :pos pos)))        
    l))
(defun khr-rleg-link2-wrl (&optional (pos (float-vector 0 0 -65)))
  (create-wrl-link "RLEG_LINK2" :joint-coord (make-cascoords :pos pos)))
(defun khr-rleg-link2 (&optional (pos (float-vector 0 0 -65)))
  (let ((am (khr-arm-link))
	(sv (khr-servo-s2552))
	l)
    (send am :rotate pi :z :world)
    (send am :rotate (deg2rad -30) :y :world)
    (send am :locate #f(0 20 -25))
    (send sv :rotate -pi/2 :x :world)
    (send sv :rotate (deg2rad -70) :y :world)
    (send sv :locate (float-vector 0 -20 -65) :world)
    (setq l (kxr-link-create (append (send am :bodies) (list sv))
			     :joint-coord (make-cascoords :pos pos)))        
    l))

(defun khr-lleg-link3-wrl (&optional (pos (float-vector 19.7 8.25 -38.5)))
  (create-wrl-link "LLEG_LINK3" :joint-coord (make-cascoords :pos pos)))
(defun khr-lleg-link3 (&optional (pos (float-vector 19.7 8.25 -38.5)))
  (let ((am (khr-arm-link))
	(sv (khr-servo-s2552))
	l)
    (send am :locate #f(0 20 -25) :world)
    (send sv :rotate pi/2 :y :world)
    (send sv :rotate pi/2 :x :world)
    (send sv :locate #f(0 8.5 -38.5) :world)
    (setq l (kxr-link-create (append (send am :bodies) (list sv))
			     :joint-coord (make-cascoords :pos pos)))        
    l))
(defun khr-rleg-link3-wrl (&optional (pos (float-vector 19.7 -8.25 -38.5)))
  (create-wrl-link "RLEG_LINK3" :joint-coord (make-cascoords :pos pos)))
(defun khr-rleg-link3 (&optional (pos (float-vector 19.7 -8.25 -38.5)))
  (let ((am (khr-arm-link))
	(sv (khr-servo-s2552))
	l)
    (send am :locate #f(0 -20 -25) :world)
    (send sv :rotate pi/2 :y :world)
    (send sv :rotate -pi/2 :x :world)
    (send sv :locate #f(0 -8.5 -38.5) :world)
    (setq l (kxr-link-create (append (send am :bodies) (list sv))
			     :joint-coord (make-cascoords :pos pos)))        
    l))

(defun khr-lleg-link4-wrl (&optional (pos (float-vector -20 0 -26)))
  (create-wrl-link "LLEG_LINK4" :joint-coord (make-cascoords :pos pos)))
(defun khr-rleg-link4-wrl (&optional (pos (float-vector -20 0 -26)))
  (create-wrl-link "RLEG_LINK4" :joint-coord (make-cascoords :pos pos)))

(defun khr-sole-sensor-plate (&key (name "khr-sole-sensor-plate"))
  (let* ((bod (make-fillet-cube 58 58 10 2))
	 (b- (make-fillet-cube 29 29 10 1))
	 (r (* 0.5 (sqrt 2) 108))
	 (f+ (make-fillet-cube 20 5 30 2))
	 (c- (make-cube 100 100 10))
	 (c-- (make-cube 100 100 40))
	 (sbod bod)
	 holes l)
    (send c- :rotate (deg2rad 45) :z :world)
    (send c-- :locate (float-vector 0 (- 11 (* 50 (sqrt 2)) 50) 0) :world)
    (setq c- (body- c- c--))
    (send f+ :locate #f(0 -55 0) :world)
    (setq c- (body+ c- f+))

    (dotimes (i 4)
      (send c- :locate (float-vector (* r (sin (* pi/2 i))) (* r (cos (* pi/2 i))) 3) :world)
      (setq bod (body- bod c-))
      (send c- :rotate -pi/2 :z :world))
    (send b- :locate #f(0 0 2) :world)
    (setq bod (body- bod b-))
    (send bod :locate #f(0 0 5) :world)
    (kxr-make-kxr-body name sbod bod holes
		       :color *kxr-servo-color*)
    ))
(defun khr-lleg-link4 (&key (pos (float-vector -20 0 -26)) shape sensor)
  (let* ((sole (kxr-sole-aux :name "khr-sole" :shape shape :scale 1.15))
	 (x- -21)
	 (triangle (make-prism (list
				(float-vector x- 0 25)
				(float-vector x- 5 30)
				(float-vector x- 11 30)
				(float-vector x- 56 6)
				(float-vector x- 56 0)
				(float-vector x- 0 0))
			       (float-vector (* -2 x-) 0 0)))
	 (tri- (make-cube (- (* -2 x-) 2) 150 30))
	 (offset (float-vector 0 0 0))
	 j sp0 sp1 blist l)
    (send sole :rotate -pi/2 :y :world)
    (send sole :rotate -pi/2 :z :world)
    ;;
    (send tri- :locate #f(0 0 16) :world)
    (send tri- :worldcoords)
    (setq triangle (body- triangle tri-))
    (send triangle :locate #f(0 -18 3) :world)
    (when
	sensor
      (setq sp0 (khr-sole-sensor-plate))
      (setq sp1 (khr-sole-sensor-plate))
      (send sp1 :rotate pi :x :world)
      (setq j (kxr-sensor-joint-base-c))
      (send j :locate #f(0 0 -6) :world)
      (send sp1 :assoc j)
      (send sp1 :locate #f(0 0 32) :world)
      (send sp0 :assoc sp1)
      (send sp0 :locate #f(0 10 -30) :world)
      (send sole :locate #f(0 0 -32) :world)
      (send sole :assoc sp0)
      (setq blist (list sp0 sp1 j))
      (setq offset #f(0 0 -32))
      )
    (send sole :assoc triangle)
    (send sole :locate (v+ offset #f(-22 10 -26)) :world)
    (setq l (kxr-link-create (append (list sole triangle) blist)
			     :joint-coord (make-cascoords :pos (v+ offset pos))))
    l))

(defun khr-rleg-link4 (&key (pos (float-vector -20 0 -26)) shape sensor)
  (let* ((sole (kxr-sole-aux :name "khr-sole" :shape shape :scale 1.15))
	 (x- -21)
	 (triangle (make-prism (list
				(float-vector x- 0 25)
				(float-vector x- 5 30)
				(float-vector x- 11 30)
				(float-vector x- 56 6)
				(float-vector x- 56 0)
				(float-vector x- 0 0))
			       (float-vector (* -2 x-) 0 0)))
	 (tri- (make-cube (- (* -2 x-) 2) 150 30))
	 (offset (float-vector 0 0 0))
	 j sp0 sp1 blist l)
    (send tri- :locate #f(0 0 16) :world)
    (send tri- :worldcoords)
    (setq triangle (body- triangle tri-))
    (send triangle :rotate pi :z :world)
    (send sole :rotate -pi/2 :y :world)
    (send sole :rotate pi/2 :z :world)
    (send triangle :locate #f(0 18 3) :world)
    (when
	sensor
      (setq sp0 (khr-sole-sensor-plate))
      (setq sp1 (khr-sole-sensor-plate))
      (send sp1 :rotate pi :x :world)
      (setq j (kxr-sensor-joint-base-c))
      (send j :locate #f(0 0 -6) :world)
      (send sp1 :assoc j)
      (send sp1 :locate #f(0 0 32) :world)
      (send sp0 :assoc sp1)
      (send sp0 :locate #f(0 -10 -30) :world)
      (send sole :locate #f(0 0 -32) :world)
      (send sole :assoc sp0)
      (setq blist (list sp0 sp1 j))
      (setq offset #f(0 0 -32))
      )
    (send sole :assoc triangle)
    (send sole :locate (v+ offset #f(-22 -10 -26)) :world)
    (setq l (kxr-link-create (append (list sole triangle) blist)
			     :joint-coord (make-cascoords :pos (v+ offset pos)))        )
    l))

;;
;;;
(defclass khr-robot :super kxr-robot :slots ())

(defun khr-robot (&rest args &key (name "khr-robot") &allow-other-keys)
  (instance* khr-robot :init :name name args))

(defmethod khr-robot
   (:setup-neck
    (&rest args &key stand neck-jf ((:body bod)) torso
	   (neck :y-p) head-b (neck-axis :p) ((:head hd)) m5stack &allow-other-keys)
  (let (n)
    (setq lst nil jlst nil)   		   
    (send self :push-joint :head "neck" :z -170 170
	  :parent-link (setq n (send self :torso-top-link))
	  :parent-name (if (get n :head) :neck :joint-coord)
	  :child-link 
	  (if hd (khr-head-link1) (khr-head-link0)))
    (when hd
      (send self :push-joint :head "neck" :y -120 120 :child-link
	    (khr-head-link2))
      ))
		  )
  (:setup-head
   (&rest args &key stand neck-jf ((:body bod)) torso (eye-length 26) (eye-y-rotate 3) (lego-bar 13)
	  (head-offset #f(0 0 0)) 3eye-rev ((:head hd)) m5stack &allow-other-keys)
   (let
       (hc hl l)
     (setq l (kxr-head-sensor hd :lego-bar lego-bar :m5stack m5stack :3eye-rev 3eye-rev))
     (when l
       (if head-offset (send l :locate head-offset))
       (send self :add-link l :offset head-offset :add-props (list :cameras))
       (dolist (c (get l :cameras)) (send (car lst) :assoc c))
       )
     (unless head-end-coords
       (cond
	 ((> (length cameras) 1) ;; stereo head
	  (if hl
	      (setq hc (get hl :joint-coord))
	      (setq hc (get (send self :neck-top-link) :joint-coord))))
	 ((null hc) (setq hc (get (car lst) :joint-coord))))
       (when hc
	 (setq head-end-coords (make-cascoords :pos (send hc :worldpos)
					       :rot (m*
						     (rotation-matrix pi/2 :y)
						     (rotation-matrix -pi/2 :z))
					       :name :head-end-coords))))
     (unless cameras
       (setq cameras (get (car lst) :cameras)))
     (cond
       ((and cameras (cdr lst) (caddr lst))
	(send (caddr lst) :assoc head-end-coords))	  
       (lst
	(send (car lst) :assoc head-end-coords))
       ((get body-link :neck)
	(send (get body-link :neck) :assoc head-end-coords)))
     ;;
     (push (cons :head head-end-coords) end-coords-alist)
     (setq head (reverse lst))
     (push (cons :head (car head)) root-link-alist)
     (push (cons :head head) lst-alist)
     (push (cons :head (reverse jlst)) jlst-alist)
     ))
  (:setup-arm
   (&rest args &key (l/r :larm) shoulder elbow wrist gripper gripper-axis passive-wheel
	  (shoulder-width 26) (upper-arm-length 26) (wrist-len 26) wrist-a
	  ((:body bod)) wrist-sensor gripper-sensor gripper-inner gripper-straight
	  arm mirrored &allow-other-keys)
   (let (ec hand)
     (setq lst nil jlst nil)
     (send self :push-joint l/r 0 :y -170 170 :parent-link (send self :torso-top-link)
	   :parent-name l/r :child-link (case l/r (:larm (khr-larm-link0)) (:rarm (khr-rarm-link0))))
     (case
	 shoulder
       ((:y :p-r-y)
	(send self :push-joint l/r 0 :x 
	      (if (eq l/r :larm) -30 -185) (if (eq l/r :larm) 185 30)
	      :child-link (if (kxr-left-limb? l/r) (khr-larm-link10) (khr-rarm-link10)))
	(send self :push-joint l/r 0 :z -180 180
	      :child-link (if (kxr-left-limb? l/r) (khr-larm-link11) (khr-rarm-link11)))
	(send self :push-joint l/r 1 :y -170 170
	      :child-link (if (kxr-left-limb? l/r) (khr-larm-link2) (khr-rarm-link2)))
	)
       (t
	(send self :push-joint l/r 0 :x 
	      (if (eq l/r :larm) -30 -185) (if (eq l/r :larm) 185 30)
	      :child-link (if (kxr-left-limb? l/r) (khr-larm-link1) (khr-rarm-link1)))
	(send self :push-joint l/r 1 :y -170 170
	      :child-link (if (kxr-left-limb? l/r) (khr-larm-link2) (khr-rarm-link2)))))
     (cond
       ((memq wrist '(t :dummy))
	(send self :add-link (if (kxr-left-limb? l/r) (khr-lwrist-link) (khr-rwrist-link))))
       (wrist (send* self :setup-wrist :l/r l/r args)))
     (cond
       (gripper (send* self :setup-gripper :l/r l/r args)
		)
       (t (send self :add-link (khr-hand-link :l/r l/r))
	  (when (get (car lst) :joint-coord)
	    (setq ec (make-cascoords
		      :rot (rotation-matrix 0 :x)
		      :pos (send (get (car lst) :joint-coord) :worldpos)
		      :name (format nil "~A-end-coords" l/r)))
	    (push (cons l/r ec) end-coords-alist)
	    (send (car lst) :assoc ec))
	  (send self :ln-ending type l/r gripper gripper-inner gripper-straight (car lst))))
     ))
  (:setup-leg
   (&rest args &key (l/r :lleg) knee-type active-wheel ankle type crotch-z  passive-wheel
	  ((:body bod)) leg ankle-sensor sole sole-sensor &allow-other-keys)
   (let (ec l j)
     (setq lst nil jlst nil)
     (cond
       ((eq bod :l2l6)
	  (send self :push-joint l/r 0 :z -90 90 :parent-link body-link
		:parent-name l/r :child-link (if (kxr-left-limb? l/r) (khr-lleg-link00) (khr-rleg-link00)))
	(send self :push-joint l/r 0 :x -90 90
	      :child-link (if (kxr-left-limb? l/r) (khr-lleg-link0) (khr-rleg-link0))))
       (t
	(send self :push-joint l/r 0 :x -90 90 :parent-link body-link
	      :parent-name l/r :child-link (if (kxr-left-limb? l/r) (khr-lleg-link0) (khr-rleg-link0)))))
     (send self :push-joint l/r 0 :y -135 135
	   :child-link (if (kxr-left-limb? l/r) (khr-lleg-link1) (khr-rleg-link1)))
     (send self :push-joint l/r 1 :y -90 180
	   :child-link (if (kxr-left-limb? l/r) (khr-lleg-link2) (khr-rleg-link2)))
     (send self :push-joint l/r 2 :y -90 180
	   :child-link (if (kxr-left-limb? l/r) (khr-lleg-link3) (khr-rleg-link3)))
     ;;
     (send self :push-joint l/r 2 :x -90 180
	   :child-link (if (kxr-left-limb? l/r) (khr-lleg-link4 :shape sole :sensor sole-sensor)
			   (khr-rleg-link4 :shape sole :sensor sole-sensor)))
     (when
	 (eq-memq passive-wheel '(:sole))
       (let* ((x -60) (cy 19) (z 10) (w/2 47))
	 (setq l (car lst) j (car jlst))
	 (send self :push-joint l/r 2 :wy -100000 100000 :parent-link l :active nil
	       :offset-v (if (kxr-left-limb? l/r)
			     (float-vector x (- cy w/2) z)
 			     (float-vector x (- (- cy w/2)) z))
	       :child-link (kxr-passive-wheel-link :axis :y :l/r l/r))
	 (send self :push-joint l/r 2 :wy -100000 100000 :parent-link l :active nil
	       :offset-v (if (kxr-left-limb? l/r)
			     (float-vector x (+ cy w/2) z)
 			     (float-vector x (- (+ cy w/2)) z))
	       :child-link (kxr-passive-wheel-link :axis :y :l/r l/r))
	 ))
     ;;
     (setq ec (make-cascoords
	       :rot (rotation-matrix 0 :x)
	       :pos (send (get (car lst) :joint-coord) :worldpos)
	       :name (format nil "~A-end-coords" l/r)))
     (push (cons l/r ec) end-coords-alist)
     (send (car lst) :assoc ec)
     (send self :ln-ending type l/r nil nil nil (car lst)) ;; :setup-leg
     ))
  (:setup-body
   (&rest args &key ((:body bod) :l2l5) ((:head hd)) stand backpack tail
	  (backpack-function 'kxr-back-pack) flat-shoulder shoulder-front neck-front crotch-front
	  wing neck-jf ((:torso trso)) (waist-cut t) &allow-other-keys)
   (setq type :l2)
   (case
       bod
     (:l2l5 (setq body-link (khr-l2l5body-link)))
     (:l2l6 (setq body-link (if trso (khr-waist22-link)
				(khr-l2l6body-link))))
     )
   (when backpack
     (let ((bp (find-if #'(lambda (x) (substringp "back-pack" (send x :name)))
			(send body-link :bodies)))
	   bds m5)
       (case
	   backpack
	 ((:m5 :m5fire)
	  (setq m5 (m5stackFIRE-bodyset))
	  )
	 ((:m5gray)
	  (setq m5 (m5stackGRAY-bodyset))
	  ))
       (send m5 :move-named-coords "connect0" bp "uph1") 
       (when m5
	 (setq bds (send body-link :bodies))
	 (dolist (b (cdr bds)) (send (car bds) :assoc b))
	 (setq (body-link . geo::bodies) (append bds (send m5 :bodies))))
       ))
   (when wing
     (let ((wing-pos #f(-18 0 0)))
       (when (memq :wing-pos args)
	 (setq wing-pos (cadr (memq :wing-pos args))))
       (require :kxrextentions)
       (send self :add-link (main-shaft-link wing-pos) :parent-name :back-panel
	     :target-link body-link
	     :add-props (list :left-joint-coord :right-joint-coord))
       ))
   ;;(send body-link :name "torso")
   body-link)
  (:setup-torso
   (&rest args &key (neck-axis :p) ((:torso trso)) wing passive-wheel
	  (waist-len 38) &allow-other-keys)
   (setq lst nil jlst nil)
   (when
       trso
     (send self :push-joint :torso "chest" :z -90 90 :parent-link body-link
	   :parent-name :torso :child-link (khr-chest-link))
     (setq torso (car lst))
     (setq torso-end-coords (get torso :joint-coord))
     (push (cons :torso torso-end-coords) end-coords-alist)
     (push (cons :torso torso) root-link-alist)
     (push (cons :torso (list torso)) lst-alist)
     (push (cons :torso (reverse jlst)) jlst-alist)
     )
   )
  )

(setq *khr-body-functions*
      '(
	))



(provide :khrmodels)

